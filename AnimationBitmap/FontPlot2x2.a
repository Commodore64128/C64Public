!zn

; Entry:
; A/X = lo/hi screen address to start drawing to
FontPlot2x2_SetScreen
	sta .w2x2ScrnOutLo
	stx .w2x2ScrnOutHi
	rts

; Entry:
; A/X = lo/hi string address to draw
.tempLen !by 0
FontPlot2x2_EntryCentered
	jsr DoLookupLanguage_AX
	sta .w2x2CharStringLo
	stx .w2x2scrChrCodeHi
	sta .w2x2CharStringLo2
	stx .w2x2scrChrCodeHi2

	ldy #0
	sty .tempLen
.w2x2CharStringLo2 = *+1
.w2x2scrChrCodeHi2 = *+2
.cl1
	lda $1234,y
	beq .endString

	and #$3f
	tax
	lda #2
	cpx #'.'
	beq .isOne
	cpx #39	; Character '
	beq .isOne
	jmp .isTwo
.isOne
	lda #1
.isTwo
	clc
	adc .tempLen
	sta .tempLen

	iny
	bne .cl1
.endString

	; Align centered
	lda #VIC2ScreenCharsWidth
	sec
	sbc .tempLen
	clc
	adc #1
	lsr
	clc
	adc .w2x2ScrnOutLo
	sta .w2x2ScrnOutLo
	bcc .lc2
	inc .w2x2ScrnOutHi
.lc2
	jmp .draw

; Entry:
; A/X = lo/hi string address to draw
FontPlot2x2_Entry
	jsr DoLookupLanguage_AX
						sta .w2x2CharStringLo
						stx .w2x2scrChrCodeHi
.draw
						ldy #$00
.write2x2_LoopStart
						sty .w2x2CharIndex

.w2x2CharStringLo   =*+$01
.w2x2scrChrCodeHi   =*+$02
.write2x2_ReadInput	 lda $1234,Y
						bne .write2x2_NotExit
						rts

.write2x2_NotExit
						ldx #$02
						stx .numCharCols

						cmp #' '
						beq .AdvanceToNext2x2Block

						cmp #'.'
						bne .notFullstop
						ldx #$01
						stx .numCharCols
						ldx #54
						jmp .doPlot2
.notFullstop
						cmp #39	; '
						bne .notApostrohe
						ldx #$01
						stx .numCharCols
						ldx #55
						jmp .doPlot2
.notApostrohe
.doPlot
						and #$3f
						asl
						tax
.doPlot2
						ldy #0
						lda .theCharMapRow1-2,x
						jsr .PlotCharIncY
						lda .numCharCols
						cmp #1
						beq .is1a
						lda .theCharMapRow1-1,x
						jsr .PlotCharIncY
.is1a
						ldy #VIC2ScreenCharsWidth
						lda .theCharMapRow2-2,x
						jsr .PlotCharIncY
						lda .numCharCols
						cmp #1
						beq .is1b
						lda .theCharMapRow2-1,x
						jsr .PlotCharIncY
.is1b

.AdvanceToNext2x2Block  clc 
						lda .w2x2ScrnOutLo
.numCharCols   =*+$01
.write2x2_NumCharsToAdvance adc #$02
						sta .w2x2ScrnOutLo
						bcc .write2x2_carryon
						inc .w2x2ScrnOutHi
.write2x2_carryon

.w2x2CharIndex = *+$01
						ldy #$0F
						iny 
						jmp .write2x2_LoopStart



.PlotCharIncY
.w2x2ScrnOutHi   =*+$02
.w2x2ScrnOutLo   =*+$01
						sta $1234,Y
.o1
						iny 
						rts 

.theCharMapRow1 = *
.theCharMapRow2 = *+54
!bin "intro_font_v01_apost_round.ctm" , 54*2 , $14 + $800 + $100 , 2
