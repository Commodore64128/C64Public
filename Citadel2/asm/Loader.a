!zn
; Entry:
; A = Level number to load, zero based
; Exit:
; A/X/Y = Trashed
LoadLevel
	lda #CartFile1_Bank_DataLevel1_prg
	jsr CartSelectBank_8K

	ldx #<CartFile1_Start_DataLevel1_prg
	ldy #>CartFile1_Start_DataLevel1_prg
	jsr MemoryCopy_SetSource

	ldx #<LevelDataStart
	ldy #>LevelDataStart
	jsr MemoryCopy_SetDestination

	ldx #<CartFile1_Size_DataLevel1_prg
	ldy #>CartFile1_Size_DataLevel1_prg
	lda #0
	jsr MemoryCopy_ForSize

	jsr CartKillBank

!ifdef Scroller_EnableBlockColourSpeedCodeInCart {
	; Because block colour data cannot be in the banked in ROM position whilst the colourise code is running
	ldx #0
.l1
	lda CharColoursSource,x
	sta CharColours,x
	dex
	bne .l1
} else {
CharColours = CharColoursSource
}

	rts

!ifdef Scroller_EnableBlockColourSpeedCodeInCart {
CharColours
	!fill 256 , 1
}


!zn
BeforeScorePanelDataHook
	lda #CartFile1_Bank_DataScorePanel_prg
	jmp CartSelectBank_8K

!zn
AfterScorePanelDataHook = CartKillBank


!zn
RefreshTitleScreenMusic
	lda #CartFile1_Bank_Citadel2_Title_prg
	jsr CartSelectBank_8K

	ldx #<CartFile1_Start_Citadel2_Title_prg
	ldy #>CartFile1_Start_Citadel2_Title_prg
	jsr MemoryCopy_SetSource

	ldx #<MusicPlayerInit
	ldy #>MusicPlayerInit
	jsr MemoryCopy_SetDestination

	ldx #<CartFile1_Size_Citadel2_Title_prg
	ldy #>CartFile1_Size_Citadel2_Title_prg
	lda #0
	jsr MemoryCopy_ForSize

	jsr CartKillBank
	rts



!zn
DisplayLoading
	sei
;	+MACROWaitForTheLastScan_A
	+ClearScreenAt_AX COLOURRAM , VIC2Colour_Black
	+BlankScreenBorderSpriteSound_A
	+MACROWaitForTheLastScan_A
	+SetDefaultScreenLowerCase_A
	+DisplayColourTextMiddle_AX .textStart_Loading , .textEnd_Loading , SCREENRAM + (9*VIC2ScreenCharsWidth) , VIC2Colour_White
	rts

.textStart_Loading
!scr "Loading..."
.textEnd_Loading





!zn
; To be called afer UpdateMapToMaze
MapCreateZones
	lda #CartFile1_Bank_ZonesLevel1_bin
	jsr CartSelectBank_8K

	ldx #<CartFile1_Start_ZonesLevel1_bin
	ldy #>CartFile1_Start_ZonesLevel1_bin
	jsr MemoryCopy_SetSource

	ldx #<Maze
	ldy #>Maze
	jsr MemoryCopy_SetDestination

	ldx #<CartFile1_Size_ZonesLevel1_bin
	ldy #>CartFile1_Size_ZonesLevel1_bin
	lda #0
	jsr MemoryCopy_ForSize

	jsr CartKillBank
	rts
