; Originally from: http://codebase64.org/doku.php?id=base:8bit_atan2_8-bit_angle
;; Calculate the angle, in a 256-degree circle, between two points.
;; The trick is to use logarithmic division to get the y/x ratio and
;; integrate the power function into the atan table. Some branching is
;; avoided by using a table to adjust for the octants.
;; In otherwords nothing new or particularily clever but nevertheless
;; quite useful.
;;
;; by Johan Forslöf (doynax)
; MPi: Now includes tweaks and fixes.

; Zero page vars
octant	= $02
x1		= $03
y1		= $05
x2		= $04
y2		= $06

!zn {
	; Up to the programmer if they want to align these or not
	;;;;;;;; atan(2^(x/32))*128/pi ;;;;;;;;

.atan_tab
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$00,$00,$00
	!by $00,$00,$00,$00,$00,$01,$01,$01
	!by $01,$01,$01,$01,$01,$01,$01,$01
	!by $01,$01,$01,$01,$01,$01,$01,$01
	!by $01,$01,$01,$01,$01,$01,$01,$01
	!by $01,$01,$01,$01,$01,$02,$02,$02
	!by $02,$02,$02,$02,$02,$02,$02,$02
	!by $02,$02,$02,$02,$02,$02,$02,$02
	!by $03,$03,$03,$03,$03,$03,$03,$03
	!by $03,$03,$03,$03,$03,$04,$04,$04
	!by $04,$04,$04,$04,$04,$04,$04,$04
	!by $05,$05,$05,$05,$05,$05,$05,$05
	!by $06,$06,$06,$06,$06,$06,$06,$06
	!by $07,$07,$07,$07,$07,$07,$08,$08
	!by $08,$08,$08,$08,$09,$09,$09,$09
	!by $09,$0a,$0a,$0a,$0a,$0b,$0b,$0b
	!by $0b,$0c,$0c,$0c,$0c,$0d,$0d,$0d
	!by $0d,$0e,$0e,$0e,$0e,$0f,$0f,$0f
	!by $10,$10,$10,$11,$11,$11,$12,$12
	!by $12,$13,$13,$13,$14,$14,$15,$15
	!by $15,$16,$16,$17,$17,$17,$18,$18
	!by $19,$19,$19,$1a,$1a,$1b,$1b,$1c
	!by $1c,$1c,$1d,$1d,$1e,$1e,$1f,$1f


	;;;;;;;; log2(x)*32 ;;;;;;;;

.log2_tab
	!by $00,$00,$20,$32,$40,$4a,$52,$59
	!by $60,$65,$6a,$6e,$72,$76,$79,$7d
	!by $80,$82,$85,$87,$8a,$8c,$8e,$90
	!by $92,$94,$96,$98,$99,$9b,$9d,$9e
	!by $a0,$a1,$a2,$a4,$a5,$a6,$a7,$a9
	!by $aa,$ab,$ac,$ad,$ae,$af,$b0,$b1
	!by $b2,$b3,$b4,$b5,$b6,$b7,$b8,$b9
	!by $b9,$ba,$bb,$bc,$bd,$bd,$be,$bf
	!by $c0,$c0,$c1,$c2,$c2,$c3,$c4,$c4
	!by $c5,$c6,$c6,$c7,$c7,$c8,$c9,$c9
	!by $ca,$ca,$cb,$cc,$cc,$cd,$cd,$ce
	!by $ce,$cf,$cf,$d0,$d0,$d1,$d1,$d2
	!by $d2,$d3,$d3,$d4,$d4,$d5,$d5,$d5
	!by $d6,$d6,$d7,$d7,$d8,$d8,$d9,$d9
	!by $d9,$da,$da,$db,$db,$db,$dc,$dc
	!by $dd,$dd,$dd,$de,$de,$de,$df,$df
	!by $df,$e0,$e0,$e1,$e1,$e1,$e2,$e2
	!by $e2,$e3,$e3,$e3,$e4,$e4,$e4,$e5
	!by $e5,$e5,$e6,$e6,$e6,$e7,$e7,$e7
	!by $e7,$e8,$e8,$e8,$e9,$e9,$e9,$ea
	!by $ea,$ea,$ea,$eb,$eb,$eb,$ec,$ec
	!by $ec,$ec,$ed,$ed,$ed,$ed,$ee,$ee
	!by $ee,$ee,$ef,$ef,$ef,$ef,$f0,$f0
	!by $f0,$f1,$f1,$f1,$f1,$f1,$f2,$f2
	!by $f2,$f2,$f3,$f3,$f3,$f3,$f4,$f4
	!by $f4,$f4,$f5,$f5,$f5,$f5,$f5,$f6
	!by $f6,$f6,$f6,$f7,$f7,$f7,$f7,$f7
	!by $f8,$f8,$f8,$f8,$f9,$f9,$f9,$f9
	!by $f9,$fa,$fa,$fa,$fa,$fa,$fb,$fb
	!by $fb,$fb,$fb,$fc,$fc,$fc,$fc,$fc
	!by $fd,$fd,$fd,$fd,$fd,$fd,$fe,$fe
	!by $fe,$fe,$fe,$ff,$ff,$ff,$ff,$ff

.octant_adjust
	!by %00111111		;; x+,y+,|x|>|y|
	!by %00000000		;; x+,y+,|x|<|y|
	!by %11000000		;; x+,y-,|x|>|y|
	!by %11111111		;; x+,y-,|x|<|y|
	!by %01000000		;; x-,y+,|x|>|y|
	!by %01111111		;; x-,y+,|x|<|y|
	!by %10111111		;; x-,y-,|x|>|y|
	!by %10000000		;; x-,y-,|x|<|y|
	
atan2
	lda x1
	sec
	sbc x2
	bcs .o1
	eor #$ff
	clc
	adc #1
.o1
	tax
	rol octant

	lda y1
	sec
	sbc y2
	bcs .o2
	eor #$ff
	clc
	adc #1
.o2
	tay
	rol octant

	lda .log2_tab,x
	sbc .log2_tab,y
	bcc .o3
	eor #$ff
	clc
	adc #1
.o3
	tax

	lda octant
	rol
	and #%111
	tay

	lda .atan_tab,x
	eor .octant_adjust,y
	rts
}
