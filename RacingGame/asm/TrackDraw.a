!zn
; high and low screen addresses
hiscr !by $c4,$c4,$c4,$c4,$c4,$c4,$c4,$c5,$c5,$c5,$c5,$c5,$c5
	!by $c6,$c6,$c6,$c6,$c6,$c6,$c6,$c7,$c7,$c7,$c7,$c7,$c7

hisc_off !by 0,0,0,0,0,0,0,1,1,1,1,1,1,2,2,2,2,2,2,2,3,3,3,3,3,3

hiscrtr !by 4,4,4,4,4,4,4,5,5,5,5,5,5,6,6,6,6,6,6,6,7,7,7,7,7,7
	!by 8,8,8,8,8,8,8,9,9,9,9,9,9,10,10,10,10,10,10,10,11,11,11,11,11,11
loscr !by 0,$28,$50,$78,$a0,$c8,$f0,$18,$40,$68,$90,$b8,$e0,$08,$30,$58,$80
	!by $a8,$d0,$f8,$20,$48,$70,$98,$c0,$e8
	!by 0,$28,$50,$78,$a0,$c8,$f0,$18,$40,$68,$90,$b8,$e0,$08,$30,$58,$80
	!by $a8,$d0,$f8,$20,$48,$70,$98,$c0,$e8


; high and low screen addresses for block plotting
xwid !by 4
ywid !by 8
ybtl !by 19
xbtl !by 19
xhot !by 4
ymax !by 20
ybt !by 0
ypl !by 0
block_dataLo	!by 0
block_dataHi	!by 0


toftl !by $00,$24,$48,$6c,$90,$b4,$d8,$fc
!by $20,$44,$68,$8c,$b0,$d4,$f8
!by $1c,$40,$64,$88,$ac,$d0,$f4
!by $18,$3c,$60,$84,$a8,$cc,$f0
!by $14,$38,$5c,$80,$a4,$c8,$ec
!by $10,$34,$58,$7c,$a0,$c4,$e8
!by $0c,$30,$54,$78,$9c,$c0,$e4
!by $08
tofth !by $50,$50,$50,$50,$50,$50,$50,$50
!by $51,$51,$51,$51,$51,$51,$51
!by $52,$52,$52,$52,$52,$52,$52
!by $53,$53,$53,$53,$53,$53,$53
!by $54,$54,$54,$54,$54,$54,$54
!by $55,$55,$55,$55,$55,$55,$55
!by $56,$56,$56,$56,$56,$56,$56
!by $57

pertaby !by 0,0,0,0,1,1,1,1,2,2,2,2,3,3,3,3,4,4,4,4,5,5,5,5
	!by 6,6,6,6,7,7,7,7,8,8,8,8
	!by 8,8,8,8,8,8,8,8,8,8,8,8
objhorizl !by 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
	!by 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
; spare
	!by 0,0,0
objhorizr !by 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
	!by 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
; spare
	!by 0,0,0


col1 !by 10
col2 !by 9
tsdx !by 0
tsdy !by 0
tsdy2 !by 0
sidel !by 0
rast !by 122
boty !by 20
lpose !by 0
rpose !by 0
skd !by 50
trckoft !by 21
trckoft2 !by 0
ancnt1 !by 0
ancnt2 !by 70
ytrack !by 0
turbo !by 0
spdom1 !by 10
spdom2 !by 0
ypost !fill 40,0
xwidt !fill 40,0
toftt !fill 40,0
corntab !fill 40,18
lpos !by 0
rpos !by 0
lsty !by 0
yfl !by 0


TrackRendered_Row			!by 0
TrackRendered_PreviousRow	!by -1

TrackRendered_Index			!fill 25
TrackRendered_IndexNext		!fill 25
TrackRendered_YPos			!fill 25
TrackRendered_XPosLeft		!fill 25
TrackRendered_XPosRight		!fill 25
TrackRendered_XPosLeftNext	!fill 25
TrackRendered_XPosRightNext	!fill 25
TrackRendered_ScrLo			!fill 25
TrackRendered_ScrHi			!fill 25

!zn
!macro MTrackDraw_EdgeChar .char {
	cpy #40
	bcs .nextChar
	lda (TrackDraw_ZP_ScreenLoHi),y
	bne .nextChar
	lda #.char
	sta (TrackDraw_ZP_ScreenLoHi),y
.nextChar
}

!macro MTrackDraw_EdgeCharInside .char {
	cpy #40
	bcs .nextChar
	lda (TrackDraw_ZP_ScreenLoHi),y
	cmp #50
	bcs .nextChar
	lda #.char
	sta (TrackDraw_ZP_ScreenLoHi),y
.nextChar
}

TrackDraw_AdvanceRow
	lda TrackDraw_ZP_ScreenLoHi
	clc
	adc #40
	sta TrackDraw_ZP_ScreenLoHi
	bcc .tdar1
	inc TrackDraw_ZP_ScreenLoHi+1
.tdar1
	rts

!zn
TrackDraw_SmoothEdgeFixup
	ldx #0

.l1
	lda TrackRendered_IndexNext,x
	bpl .gotNext

.calculateNextRow
	; Start from the current position
	ldy TrackRendered_Index,x
	lda ypost,y

.l2
	cpy #35
	beq .gotNextY
	cmp ypost,y
	bne .gotNextY
	iny
	jmp .l2

.gotNextY
	; Calculate the next positions
	lda #19
	sec
	sbc xwidt,y
	clc
	adc toftt,y
	sta TrackRendered_XPosLeftNext,x

	lda #21
	clc
	adc xwidt,y
	clc
	adc toftt,y
	sta TrackRendered_XPosRightNext,x

.gotNext
	inx
	cpx TrackRendered_Row
	beq .ret
	jmp .l1
.ret
	rts


!source "asm/TrackDraw_EdgeLeft.a"
!source "asm/TrackDraw_EdgeRight.a"

!zn

; MPi: TODO: This takes more than a frame
; This is an exceptionally good candidate for speed code in cart banks
TrackDraw_colours
	lda topScreenBank
	beq .displaying_c0

	ldx #0

.cia1 ldy $c400,x
	lda RoadsideObjectColourLookup,y
	sta $d800,x
	inx
	bne .cia1

.cia2 ldy $c500,x
	lda RoadsideObjectColourLookup,y
	sta $d900,x
	inx
	bne .cia2

.cia3 ldy $c600,x
	lda RoadsideObjectColourLookup,y
	sta $da00,x
	inx
	bne .cia3

	ldx #$47
.cia4 ldy $c700,x
	lda RoadsideObjectColourLookup,y
	sta $db00,x
	dex
	bpl .cia4
	rts

.displaying_c0
	ldx #0

.cia1b ldy $c000,x
	lda RoadsideObjectColourLookup,y
	sta $d800,x
	inx
	bne .cia1b

.cia2b ldy $c100,x
	lda RoadsideObjectColourLookup,y
	sta $d900,x
	inx
	bne .cia2b

.cia3b ldy $c200,x
	lda RoadsideObjectColourLookup,y
	sta $da00,x
	inx
	bne .cia3b

	ldx #$47
.cia4b ldy $c300,x
	lda RoadsideObjectColourLookup,y
	sta $db00,x
	dex
	bpl .cia4b
	rts


TrackDraw_setup

	ldx topScreenBank
	lda BankToAddrOffscreen,x
	sta .sm1+1

	lda #0
	sta TrackRendered_Row
	lda #-1
	sta TrackRendered_PreviousRow

	ldx #24
.up1
	lda hisc_off,x
.sm1	ora #0
	sta hiscr,x
	dex
	bpl .up1

	ldx trckoft
	lda hiscrtr,x
	clc
	adc #(>Data_trackconfig2)-4
	sta TrackDraw_ZP_TrackLoHi+1
	lda loscr,x
	sta TrackDraw_ZP_TrackLoHi
	ldy #37
	lda (TrackDraw_ZP_TrackLoHi),y
	tax
	asl
	asl
	asl
	clc
	adc #VIC2SpriteYBorderTop
	sta rast
l5x
	lda loscr,x
	sta l5y+1
	lda hiscr,x
	sta l5y+2
	ldy #39
	lda #0
l5y	sta $c000,y
	dey
	bpl l5y
	dex
	bpl l5x

	ldy #39
l5
	lda (TrackDraw_ZP_TrackLoHi),y
	sta ypost,y
	dey
	bpl l5
	lda TrackDraw_ZP_TrackLoHi+1
	clc
	adc #8
	sta TrackDraw_ZP_TrackLoHi+1
	lda #255
	sta lsty
	ldy #39
l6
	lda (TrackDraw_ZP_TrackLoHi),y
	sta xwidt,y
	dey
	bpl l6

	; Setup the bottom clipping position to be any hill crest
	ldx ypost+38
	lda ypost,x
	sta ymax

	rts

!zn
TrackDraw_chars

	; Start drawing from the far row
	ldy #0
	sty yfl
l7
;	+WaitForFire_A

	ldy yfl

	; Once the hill crest has been encountered then set the maximum screen height
	cpy ypost+38
	bcc .notNearCrest
	lda #20
	sta ymax
.notNearCrest

	lda ypost,y
	cmp lsty
	bne l10lo
	jmp l10

.ll10
	lda #-1
	sta TrackRendered_PreviousRow
	jmp l10

l10lo
	; Row index >= any crest? Then skip the y pos check
	cpy ypost+38
	bcs l10a

	; Row y pos >= crest? Then skip drawing the row
	cmp ymax
	bcs .ll10
l10a
	ldx TrackRendered_Row
	sta TrackRendered_YPos,x

	tax
	stx lsty

	lda loscr,x
	sta TrackDraw_ZP_ScreenLoHi
	lda hiscr,x
	sta TrackDraw_ZP_ScreenLoHi+1
;	cpx #20
;	bcs l10
	lda #19
	sec
	sbc xwidt,y
	clc
	adc toftt,y
	sta lpos

	ldx TrackRendered_Row
	sta TrackRendered_XPosLeft,x

	; Store the row index
	tya
	sta TrackRendered_Index,x

	lda #21
	clc
	adc xwidt,y
	clc
	adc toftt,y
	sta rpos

	sta TrackRendered_XPosRight,x
	inc TrackRendered_Row

	lda TrackDraw_ZP_ScreenLoHi
	sta TrackRendered_ScrLo,x
	lda TrackDraw_ZP_ScreenLoHi+1
	sta TrackRendered_ScrHi,x

	lda #-1
	sta TrackRendered_IndexNext,x

	ldy TrackRendered_PreviousRow
	bmi .noValidPrevious

	; Update any next values from the previous row
	lda TrackRendered_XPosRight,x
	sta TrackRendered_XPosRightNext,y
	lda TrackRendered_XPosLeft,x
	sta TrackRendered_XPosLeftNext,y

	txa
	sta TrackRendered_IndexNext,y

.noValidPrevious

	stx TrackRendered_PreviousRow

	; Clear the current line and draw the road span all in one go
	lda #0
	ldy #39
.rsl1
	cpy rpos
	bmi .doRoad1
	sta (TrackDraw_ZP_ScreenLoHi),y
	dey
	bpl .rsl1
	jmp l10

.doRoad1
	lda #1
.rsl2
	cpy lpos
	bmi .doGround1
	sta (TrackDraw_ZP_ScreenLoHi),y
	dey
	bpl .rsl2
	jmp l10

.doGround1
	lda #0
.rsl3
	sta (TrackDraw_ZP_ScreenLoHi),y
	dey
	bpl .rsl3

	
l10
	ldy yfl
	jsr objsprint
	inc yfl
	lda yfl
	cmp #36
	beq loop
	jmp l7
loop
	rts

TrackDraw_offscreen
loop2
	lda 56320
	and #1
	bne m1z
	dec spdom1
	bne m1b
	lda #10
	sta spdom1
	lda spdom2
	cmp #3
	beq m1b
	inc spdom2
	jmp m1b
m1z
	dec spdom1
	bne m1b
	lda #10
	sta spdom1
	lda spdom2
	beq m1b
	dec spdom2
m1b
	lda 56320
	and #8
	bne m1
	jsr left
	jmp loopr
m1
	lda 56320
	and #4
	bne m2
	jsr right
	jmp loopr
m2	;jsr animback
loopr
	ldy spdom2
	beq loopr2
loopr3
	jsr advance

	lda HorizonXPostion
	clc
	adc lastCorner
	sta HorizonXPostion

	dey
	bne loopr3

loopr2
	jsr cornoftfig
	jsr trackoftfig
	ldx trckoft2
loop2z
	lda tabletrck,x
	sta trckoft
	rts

tabletrck = Data_trckypos
tablecorn = Data_trckcornpos

lastCorner !by 0

cornoftfig
	ldy trckoft2
	lda tablecorn,y
	sec
	sbc #19
	cmp #$80
	ror
	cmp #$80
	ror
	sta lastCorner

	ldx tablecorn,y
	cpx #19
	bcs rcorn
	lda hiscrtr,x
	clc
	adc #(>Data_cornoft)-4
	sta tcf1+2
	lda loscr,x
	sta tcf1+1
	ldx #35
tcf1 lda $5000,x
	sta corntab,x
	dex
	bpl tcf1
	rts

rcorn
	txa
	sec
	sbc #19
	sta cb1+1
	lda #18
	sec
cb1 sbc #0
	tax
	lda hiscrtr,x
	clc
	adc #(>Data_cornoft)-4
	sta tcf2+2
	lda loscr,x
	sta tcf2+1
	ldx #35
tcf2 lda $5000,x
	sta corntab,x
	lda #0
	sec
	sbc corntab,x
	sta corntab,x
	dex
	bpl tcf2
	rts

trackoftfig
	ldx skd
	cpx #51
	bcs t1

	lda #50
	sec
	sbc skd
	tax
	lda toftl,x
	sta t2b+1
	lda tofth,x
	sta t2b+2

	ldx #35
t2
	lda #0
	sec
t2b	sbc $5000,x
	clc
	adc corntab,x
	sta toftt,x
	dex
	bpl t2
	rts

t1
	lda skd
	sec
	sbc #50
	tax
	lda toftl,x
	sta t3+1
	lda tofth,x
	sta t3+2
	ldx #35

t3	lda $5000,x
	clc
	adc corntab,x
	sta toftt,x
	dex
	bpl t3
	rts

left dec skd
;	jsr canimr
	lda skd
	cmp #$ff
	bne m4
	lda #0
	sta skd
m4 rts
right inc skd
;	jsr caniml
	lda skd
	cmp #101
	bne m5
	lda #100
	sta skd
m5 rts


blockplot
	lda block_dataLo
	sta blps1+1
	lda block_dataHi
	sta blps1+2

	lda ywid
	sta ybt
	lda ybtl
	sec
	sbc ywid
	sta ypl
	inc ypl
bl1
	ldx ypl
	cpx ymax
	bcs bl2
	lda loscr,x
	sta blpt1+1
	lda hiscr,x
	sta blpt1+2

	lda xbtl
	sec
	sbc xhot
	tay
	ldx #0
bl3
	cpy #40
	bcs bl3b
blps1 lda $8000,x
	beq bl3b
blpt1 sta $0400,y
bl3b
	iny
	inx
	cpx xwid
	bne bl3
bl2
	inc ypl
	lda blps1+1
	clc
	adc xwid
	sta blps1+1
	bcc .noC1
	inc blps1+2
.noC1
	dec ybt
	bne bl1

blend
	rts

objsprint
	sty tsdy
	ldx objhorizl,y
	beq l10g

	dex
	txa
	clc
	adc pertaby,y
	tax
	stx tsdx
	lda ypost,y
	sta ybtl
	dec ybtl
	lda RoadObject_width,x
	sta xwid
	lda RoadObject_height,x
	sta ywid

	lda RoadObject_dataLo,x
	sta block_dataLo
	lda RoadObject_dataHi,x
	sta block_dataHi

	lda #19
	sec
	sbc xwidt,y
	clc
	adc toftt,y
	sta xbtl
	lda RoadObject_hotLeft,x
	sta xhot
	jsr blockplot

	; Now the right hand road edge
l10g
	ldy tsdy
	ldx objhorizr,y
	beq l10gz
	dex
	txa
	clc
	adc pertaby,y
	tax
	stx tsdx
	lda ypost,y
	sta ybtl
	dec ybtl
	lda RoadObject_width,x
	sta xwid
	lda RoadObject_height,x
	sta ywid

	lda RoadObject_dataLo,x
	sta block_dataLo
	lda RoadObject_dataHi,x
	sta block_dataHi

	lda #21
	clc
	adc xwidt,y
	clc
	adc toftt,y
	sta xbtl
oi5	lda RoadObject_hotRight,x
	sta xhot
	jsr blockplot
l10gz	rts


advance
	ldx #34
ad1
	lda objhorizl,x
	sta objhorizl+1,x
	lda objhorizr,x
	sta objhorizr+1,x
	dex
	bpl ad1

	ldx #34
ad2
	lda objhorizl,x
	sta objhorizl+1,x
	lda objhorizr,x
	sta objhorizr+1,x
	dex
	cpx #13
	bne ad2

	lda #0
	sta objhorizr+14
	sta objhorizl+14
	ldx sidel
	lda $7a00,x
	sta objhorizl
	lda $7b00,x
	sta objhorizr
	inc sidel
	inc trckoft2
	rts
